import ch.so.agi.gretl.tasks.*
import java.nio.file.Paths
import de.undercouch.gradle.tasks.download.Download

def pathToTempFolder = System.getProperty("java.io.tmpdir")
def pathToUnzipFolder = Paths.get(pathToTempFolder, 'unzip_data')
def pathToDataZip = Paths.get(pathToTempFolder, "PLZO_INTERLIS_LV95.zip")
def xtfFilePath = Paths.get(pathToUnzipFolder.toString(), 'PLZO_INTERLIS_LV95', 'PLZO_ITF_LV95.itf')

def GROUP = "PLZ/Ortschaft-Import"

task download(type: Download) {
    group = GROUP
    description = "Download PLZ/Ortschaft."
    src 'http://data.geo.admin.ch/ch.swisstopo-vd.ortschaftenverzeichnis_plz/PLZO_INTERLIS_LV95.zip'
    dest pathToTempFolder
    overwrite true

    doLast {
        println "File downloaded to: " + pathToTempFolder
    }    
}

task unzipData(type: Copy, dependsOn: 'download') {
    group = GROUP
    description = "Unzip heruntergeladene Daten (data.zip)."
    from zipTree(pathToDataZip)
    into file(pathToUnzipFolder)
    include "**/*.itf"

    doLast {
        println "File unzipped to directory: " + pathToUnzipFolder
    }    
}

task dbImport(type: Ili2pgReplace, dependsOn: 'unzipData') {
    group = GROUP
    description = "Import plz_ortschaften-Daten in die Datenbank."
    database = [dbUriOereb, dbUserOereb, dbPwdOereb]
    dbschema = dbSchema
    models = iliModelPLZOrtschaft
    dataFile = file(xtfFilePath)
    dataset = 'ch.swisstopo.plzortschaft'
    disableValidation = true

    doLast {
        println "Data imported into db: " + dbUriOereb
    }    
}
